require "./json_encoder"
require "./quoting"

module Jennifer
  module Adapter
    abstract class BaseSQLGenerator
      module ClassMethods
        # Generates query for inserting new record to db
        abstract def insert(obj : Model::Base)
        abstract def json_path(path : QueryBuilder::JSONSelector)
        abstract def insert_on_duplicate(table, fields, rows : Int32, unique_fields, on_conflict)

        # Generates SQL for VALUES reference in `INSERT ... ON DUPLICATE` query.
        abstract def values_expression(field : Symbol)
      end

      extend ClassMethods
      extend Quoting

      def self.explain(query)
        "EXPLAIN #{self.select(query)}"
      end

      # Generates insert query
      def self.insert(table, hash)
        String.build do |s|
          s << "INSERT INTO " << table << "("
          hash.keys.join(s, ", ")
          s << ") VALUES (" << escape_string(hash.size) << ")"
        end
      end

      def self.bulk_insert(table : String, field_names : Array(String), rows : Int32)
        String.build do |s|
          s << "INSERT INTO " << table << "("
          field_names.join(s, ", ") { |e| s << e }
          s << ") VALUES "
          escaped_row = "(" + escape_string(field_names.size) + ")"
          rows.times.join(s, ", ") { s << escaped_row }
        end
      end

      def self.bulk_insert(table : String, field_names : Array(String), rows : Array)
        String.build do |s|
          s << "INSERT INTO " << table << "("
          field_names.join(s, ", ") { |e| s << e }
          s << ") VALUES "

          rows.each_with_index do |row, index|
            s << ',' if index != 0
            s << '('
            row.each_with_index do |col, col_index|
              s << ',' if col_index != 0
              s << quote(col)
            end
            s << ')'
          end
        end
      end

      # Generates common select SQL request
      def self.select(query, exact_fields = [] of String)
        String.build do |s|
          with_clause(s, query)
          select_clause(s, query, exact_fields)
          from_clause(s, query)
          body_section(s, query)
        end
      end

      def self.truncate(table : String)
        "TRUNCATE #{table}"
      end

      def self.delete(query)
        String.build do |s|
          s << "DELETE "
          from_clause(s, query)
          body_section(s, query)
        end
      end

      def self.exists(query)
        String.build do |s|
          with_clause(s, query)
          s << "SELECT EXISTS(SELECT 1 "
          from_clause(s, query)
          body_section(s, query)
          s << ")"
        end
      end

      def self.count(query)
        String.build do |s|
          with_clause(s, query)
          s << "SELECT COUNT(*) "
          from_clause(s, query)
          body_section(s, query)
        end
      end

      def self.update(obj : Model::Base)
        esc = escape_string(1)
        String.build do |s|
          s << "UPDATE " << obj.class.table_name << " SET "
          obj.arguments_to_save[:fields].map { |f| "#{f}= #{esc}" }.join(s, ", ")
          s << " WHERE " << obj.class.primary_field_name << " = " << esc
        end
      end

      def self.update(query, options : Hash)
        esc = escape_string(1)
        String.build do |s|
          s << "UPDATE " << query.table << " SET "
          options.map { |k, _| "#{k}= #{esc}" }.join(s, ", ")
          s << ' '
          body_section(s, query)
        end
      end

      def self.modify(q, modifications : Hash)
        String.build do |s|
          s << "UPDATE " << q.table << " SET "
          modifications.each_with_index do |(field, value), i|
            s << ", " if i != 0
            s << field_assign_statement(field.to_s, value)
          end
          s << ' '
          body_section(s, q)
        end
      end

      # ========== SQL clauses ================

      def self.body_section(io : String::Builder, query)
        join_clause(io, query)
        where_clause(io, query)
        group_clause(io, query)
        order_clause(io, query)
        limit_clause(io, query)
        having_clause(io, query)
        lock_clause(io, query)
        union_clause(io, query)
      end

      def self.union_clause(io : String::Builder, query)
        return unless query._unions?

        query._unions!.each do |union_tuple|
          io << " UNION "
          io << "ALL " if union_tuple[:all]
          io << self.select(union_tuple[:query])
        end
      end

      def self.lock_clause(io : String::Builder, query)
        return if query._lock.nil?

        io << ' '
        io << (query._lock.is_a?(String) ? query._lock : "FOR UPDATE")
        io << ' '
      end

      # Generates `SELECT` query clause.
      def self.select_clause(io : String::Builder, query, exact_fields : Array = [] of String)
        io << "SELECT "
        io << "DISTINCT " if query._distinct
        if !query._raw_select
          table = query._table
          if !exact_fields.empty?
            exact_fields.join(io, ", ") { |f| io << "#{table}.#{f}" }
          else
            query._select_fields.join(io, ", ") { |f| io << f.definition(self) }
          end
        else
          io << query._raw_select.not_nil!
        end
        io << ' '
      end

      def self.from_clause(io : String::Builder, from : String)
        io << "FROM " << from << ' '
      end

      # Generates `FROM` query clause.
      def self.from_clause(io : String::Builder, query)
        if query._from
          io << "FROM ( " <<
            if query._from.is_a?(String)
              query._from
            else
              if query.is_a?(QueryBuilder::ModelQuery)
                self.select(query._from.as(QueryBuilder::ModelQuery))
              else
                self.select(query._from.as(QueryBuilder::Query))
              end
            end
          io << " ) "
        elsif !query._table.empty?
          from_clause(io, query._table)
        end
      end

      # Generates `GROUP BY` query clause.
      def self.group_clause(io : String::Builder, query)
        return unless query._groups?

        io << "GROUP BY "
        query._groups!.each.join(io, ", ") { |c| io << c.as_sql(self) }
        io << ' '
      end

      # Generates `HAVING` query clause.
      def self.having_clause(io : String::Builder, query)
        return unless query._having

        io << "HAVING " << query._having.not_nil!.as_sql(self) << ' '
      end

      # Generates `JOIN` query clause.
      def self.join_clause(io : String::Builder, query)
        return unless query._joins?

        query._joins!.join(io, " ") { |j| io << j.as_sql(self) }
      end

      # Generates `WHERE` query clause.
      def self.where_clause(io : String::Builder, query : QueryBuilder::Query | QueryBuilder::ModelQuery)
        where_clause(io, query.tree.not_nil!) if query.tree
      end

      # ditto
      def self.where_clause(io : String::Builder, tree)
        return unless tree

        io << "WHERE " << tree.not_nil!.as_sql(self) << ' '
      end

      # Generates `LIMIT` clause.
      def self.limit_clause(io : String::Builder, query)
        io.print "LIMIT ", query._limit.not_nil!, ' ' if query._limit
        io.print "OFFSET ", query._offset.not_nil!, ' ' if query._offset
      end

      # Generates `ORDER BY` clause.
      def self.order_clause(io : String::Builder, query)
        return unless query._order?

        io << "ORDER BY "
        query._order!.join(io, ", ") { |expression| io.print expression.as_sql(self) }
        io << ' '
      end

      # Generates `WITH` clause.
      def self.with_clause(io : String::Builder, query)
        return unless query._ctes?

        io << "WITH "
        if query._ctes!.any?(&.recursive?)
          io << "RECURSIVE "
        end

        query._ctes!.each_with_index do |cte, index|
          cte_query = cte.query
          io << ", " if index != 0
          io << cte.name << " AS ("
          io <<
            if cte_query.is_a?(QueryBuilder::ModelQuery)
              self.select(cte_query.as(QueryBuilder::ModelQuery))
            else
              self.select(cte_query.as(QueryBuilder::Query))
            end
          io << ") "
        end
      end

      # Returns `CAST` expression.
      def self.cast_expression(expression, type : String)
        "CAST(#{expression.as_sql(self)} AS #{type})"
      end

      # ======== utils

      def self.order_expression(expression : QueryBuilder::OrderExpression)
        "#{expression.criteria.identifier} #{expression.direction}"
      end

      # Converts operator to SQL.
      def self.operator_to_sql(operator : Symbol)
        case operator
        when :like, :ilike
          "LIKE"
        when :not_like
          "NOT LIKE"
        when :regexp
          "REGEXP"
        when :not_regexp
          "NOT REGEXP"
        when :==
          "="
        when :is
          "IS"
        when :is_not
          "IS NOT"
        else
          operator.to_s
        end
      end

      def self.parse_query(query : String, args : Array(DBAny))
        args.each_with_index do |arg, i|
          args[i] = arg.as(Time).to_utc if arg.is_a?(Time)
        end
        {query % Array.new(args.size, "?"), args}
      end

      private def self.field_assign_statement(field, _value : DBAny)
        "#{field} = #{escape_string(1)}"
      end

      private def self.field_assign_statement(field, value : QueryBuilder::Statement)
        "#{field} = #{value.as_sql(self)}"
      end
    end
  end
end
